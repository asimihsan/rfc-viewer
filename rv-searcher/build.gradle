import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

shadowJar {
    archiveBaseName = project.name
    archiveClassifier = null
    archiveVersion = project.version
    mergeServiceFiles()
    transform(Log4j2PluginsCacheFileTransformer)
}

jar {
    manifest {
        attributes('Main-Class': 'art.kittencat.lambda.Handler')
    }
}

group 'art.kittencat'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'
    implementation 'com.amazonaws:aws-lambda-java-log4j2:1.5.1'
    implementation 'com.github.luben:zstd-jni:1.5.2-5'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.apache.lucene:lucene-analyzers-common:8.11.2'
    implementation 'org.apache.lucene:lucene-core:9.4.1'
    implementation 'org.apache.lucene:lucene-highlighter:9.4.1'
    implementation 'org.apache.lucene:lucene-queryparser:9.4.1'
    implementation 'org.apache.lucene:lucene-suggest:9.4.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

task copyRuntimeDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/dependency'
}


task createIndex(type: JavaExec) {
    group = "Execution"
    description = "Create index"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "art.kittencat.CreateIndex"
}

task exampleQuery(type: JavaExec) {
    group = "Execution"
    description = "Run query"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "art.kittencat.ExampleQuery"
}

build.dependsOn copyRuntimeDependencies
build.dependsOn shadowJar